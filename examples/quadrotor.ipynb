{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2f185439",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Pkg; Pkg.activate(joinpath(@__DIR__, \"..\"))\n",
    "using LinearAlgebra\n",
    "using HybridRobotDynamics\n",
    "using HiLQR"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6b29b8c8",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Quaternion Utils\n",
    "\"\"\"\n",
    "\n",
    "const Hquat = [zeros(1, 3); I(3)]\n",
    "\n",
    "function skew(a::Vector)\n",
    "    @assert size(a) == (3,)\n",
    "    return [\n",
    "        0.0 -a[3] a[2];\n",
    "        a[3] 0.0 -a[1];\n",
    "        -a[2] a[1] 0.0\n",
    "    ]\n",
    "end\n",
    "\n",
    "function L_or_R(q::Vector, is_L::Bool)::Matrix\n",
    "    @assert size(q) == (4,)\n",
    "    s = q[1]\n",
    "    v = q[2:4]\n",
    "    sign = is_L ? 1 : -1\n",
    "    return [s -v'; v s*I(3) + sign*skew(v)]\n",
    "end\n",
    "\n",
    "function Lquat(q::Vector)::Matrix\n",
    "    return L_or_R(q, true)\n",
    "end\n",
    "\n",
    "function Rquat(q::Vector)::Matrix\n",
    "    return L_or_R(q, false)\n",
    "end\n",
    "\n",
    "function Gquat(q::Vector)::Matrix\n",
    "    return Lquat(q) * Hquat\n",
    "end\n",
    "\n",
    "function Qquat(q::Vector)::Matrix\n",
    "    return Hquat' * Rquat(q)' * Lquat(q) * Hquat\n",
    "end\n",
    "\n",
    "function random_unit_quat()::Vector{Float64}\n",
    "    unscaled_quat = randn(4)\n",
    "    return unscaled_quat / norm(unscaled_quat)\n",
    "end\n",
    "\n",
    "nothing"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "498210b2",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "13-State Quadrotor Model\n",
    "\"\"\"\n",
    "\n",
    "function get_quadrotor_model(\n",
    "    gravity::Float64,\n",
    "    mass::Float64,\n",
    "    principal_moments::Vector{Float64},\n",
    "    torque_consts::Vector{Float64},\n",
    "    rotor_xs::Vector{Float64},\n",
    "    rotor_ys::Vector{Float64},\n",
    ")::HybridSystem\n",
    "    @assert size(principal_moments) == (3,)\n",
    "    @assert size(torque_consts) == (4,)\n",
    "    @assert size(rotor_xs) == (4,)\n",
    "    @assert size(rotor_ys) == (4,)\n",
    "\n",
    "    # Constants\n",
    "    g = [0.0, 0.0, -gravity]\n",
    "    J = diagm(principal_moments)\n",
    "\n",
    "    # Control allocation\n",
    "    K = [zeros(2,4); ones(1,4)]\n",
    "    B = [\n",
    "        rotor_ys';\n",
    "        -rotor_xs';\n",
    "        (torque_consts .* [-1.0, 1.0, -1.0, 1.0])'\n",
    "    ]\n",
    "\n",
    "    function quadrotor_flow(x::Vector, u::Vector)::Vector\n",
    "        p = x[1:3]\n",
    "        q = x[4:7]\n",
    "        v = x[8:10]\n",
    "        ω = x[11:13]\n",
    "        return [\n",
    "            Qquat(q) * v;\n",
    "            0.5 * Gquat(q) * ω;\n",
    "            Qquat(q)'*g + K*u/mass - cross(ω, v);\n",
    "            J \\ (B*u - cross(ω, J*ω))\n",
    "        ]\n",
    "    end\n",
    "\n",
    "    # Return HybridSystem\n",
    "    nx = 13\n",
    "    nu = 4\n",
    "    mI = HybridMode(quadrotor_flow)\n",
    "    modes = Dict(:flight => mI)\n",
    "    transitions = Dict(Tuple{Symbol, Transition}[])\n",
    "    return HybridSystem(nx, nu, transitions, modes)\n",
    "end\n",
    "\n",
    "nothing"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "365ddf6d",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Solver Setup\n",
    "\"\"\"\n",
    "\n",
    "# Crazyflie 2.1 parameters: https://arxiv.org/pdf/1608.05786\n",
    "g = 9.81\n",
    "m = 0.027\n",
    "Jdiag = [1.436e-5, 1.395e-5,  2.173e-5]\n",
    "b = 7.9379e-12 / 3.1582e-10 * ones(4)\n",
    "c = 0.0283 * ones(4)\n",
    "d = 0.0283 * ones(4)\n",
    "system = get_quadrotor_model(g, m, Jdiag, b, c, d)\n",
    "\n",
    "# Stage and terminal costs\n",
    "Q = 1e-6 * diagm([2e1*ones(3); 1e1*ones(4); ones(3); ones(3)])\n",
    "R = 1e-6 * I(system.nu)\n",
    "Qf = 1e3 * Q\n",
    "stage(x, u) = x'*Q*x + u'*R*u\n",
    "terminal(x) = x'*Qf*x\n",
    "\n",
    "# RK4 integrator\n",
    "rk4 = ExplicitIntegrator(:rk4)\n",
    "\n",
    "# Problem parameters\n",
    "N = 50\n",
    "Δt = 0.02\n",
    "params = HiLQR.Parameters(system, stage, terminal, rk4, N, Δt)\n",
    "\n",
    "# Reference trajectory\n",
    "pref = [2.0, 2.0, 2.0]\n",
    "qref = [1.0; zeros(3)]\n",
    "xref = [pref; qref; zeros(6)]\n",
    "uref = zeros(system.nu)\n",
    "params.xrefs = [xref for k = 1:N]\n",
    "params.urefs = [uref for k = 1:(N-1)]\n",
    "\n",
    "# Initial conditions\n",
    "p0 = zeros(3)\n",
    "q0 = qref #random_unit_quat()\n",
    "v0 = zeros(3)\n",
    "ω0 = zeros(3)\n",
    "params.x0 = [p0; q0; v0; ω0]\n",
    "params.mI = :flight\n",
    "\n",
    "nothing"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e1d5d79c",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Solve using HiLQR\n",
    "\"\"\"\n",
    "\n",
    "# Warm-start\n",
    "warmstart = true\n",
    "sol = HiLQR.Solution(params)\n",
    "if warmstart\n",
    "    pxs = Vector(range(p0[1], pref[1], N))\n",
    "    pys = Vector(range(p0[2], pref[2], N))\n",
    "    pzs = Vector(range(p0[3], pref[3], N))\n",
    "    for k = 1:N\n",
    "        sol.xs[k] = [pxs[k]; pys[k]; pzs[k]; zeros(10)]\n",
    "    end\n",
    "end\n",
    "\n",
    "# Solve\n",
    "cache = HiLQR.Cache(params)\n",
    "@time HiLQR.solve!(sol, cache, params; multishoot=true, max_iter=100)\n",
    "\n",
    "# Visualize states\n",
    "xs = reduce(vcat, sol.xs)\n",
    "plot_2d_states(\n",
    "    N, system.nx, (1,3), xs;\n",
    "    xlim=(-2,4), ylim=(-2,4),\n",
    "    xlabel=\"x\", ylabel=\"z\"\n",
    ")\n",
    "\n",
    "nothing"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.9",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.9"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
