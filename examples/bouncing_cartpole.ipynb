{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d872f906",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Pkg; Pkg.activate(joinpath(@__DIR__, \"..\"))\n",
    "using LinearAlgebra\n",
    "using Revise\n",
    "using HybridRobotDynamics\n",
    "using SiLQR\n",
    "using Plots\n",
    "gr()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b375e27a",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Animation Util\n",
    "\"\"\"\n",
    "\n",
    "function animate_cartpole(xs::Vector{Vector{Float64}}, qmin, qmax, l, w, draw_walls=false; filename=\"cartpole.gif\")\n",
    "    h = 0.25  # cart height\n",
    "\n",
    "    anim = Animation()  # initialize animation object\n",
    "\n",
    "    for x in xs\n",
    "        q_c, θ = x[1], x[2]\n",
    "\n",
    "        # Cart body\n",
    "        x_cart = [q_c - w/2, q_c + w/2, q_c + w/2, q_c - w/2, q_c - w/2]\n",
    "        y_cart = [0, 0, h, h, 0]\n",
    "\n",
    "        # Pole\n",
    "        pole_x = [q_c, q_c + l * sin(θ)]\n",
    "        pole_y = [h/2, h/2 - l * cos(θ)]\n",
    "\n",
    "        p = plot(; xlims=(qmin-1.0, qmax+1.0), ylims=(-0.5, l+h+0.5), legend=false, aspect_ratio=1)\n",
    "        plot!(p, x_cart, y_cart, lw=2, fillalpha=0.4, color=:blue)\n",
    "        plot!(p, pole_x, pole_y, lw=3, color=:black)\n",
    "        #scatter!(p, [q_c-w/2], [0], color=:black, markersize=4)\n",
    "        #scatter!(p, [q_c+w/2], [0], color=:black, markersize=4)\n",
    "\n",
    "        if (draw_walls)\n",
    "            vline!(p, [qmin, qmax], lw=1, lc=:red, linestyle=:dash)\n",
    "        end\n",
    "\n",
    "        frame(anim, p)\n",
    "    end\n",
    "\n",
    "    gif(anim, filename, fps=20)\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "Cartpole Model\n",
    "\"\"\"\n",
    "\n",
    "function bouncing_cartpole_model(\n",
    "    mc::Float64,\n",
    "    mp::Float64,\n",
    "    l::Float64, # pole length\n",
    "    w::Float64, # cart width\n",
    "    qmin::Float64,\n",
    "    qmax::Float64,\n",
    "    e::Float64,\n",
    "    g::Float64 = 9.81\n",
    ")::HybridSystem\n",
    "\n",
    "    nx = 4  # state dimension\n",
    "    nu = 1  # control input: force on cart\n",
    "\n",
    "    function cartpole_flow(x::Vector, u::Vector)::Vector\n",
    "        θ = x[2]\n",
    "        θ̇ = x[4]\n",
    "\n",
    "        M = [mc+mp mp*l*cos(θ); mp*l*cos(θ) mp*l^2]\n",
    "        C = [0.0 -mp*l*θ̇*sin(θ); zeros(1, 2)]\n",
    "        τ = [0, -mp*g*l*sin(θ)]\n",
    "        B = [1.0, 0.0]\n",
    "\n",
    "        q̇ = x[3:4]\n",
    "        q̈ = M \\ (τ + B*u[1] - C*q̇)\n",
    "        return [q̇; q̈]\n",
    "    end\n",
    "\n",
    "    # Continuous flow\n",
    "    flow = (x, u) -> cartpole_flow(x, u)\n",
    "\n",
    "    free_mode = HybridMode(flow)\n",
    "    modes = Dict(:free => free_mode)\n",
    "\n",
    "    # --- Guards ---\n",
    "    g_left_cart  = x -> (x[1] - w / 2) - qmin\n",
    "    g_right_cart = x -> qmax - (x[1] + w / 2)\n",
    "\n",
    "    function pole_tip_pos(x)\n",
    "        x_cart = x[1]\n",
    "        θ = x[2]\n",
    "        return x_cart + l*sin(θ)\n",
    "    end\n",
    "\n",
    "    g_left_pole  = x -> pole_tip_pos(x) - qmin\n",
    "    g_right_pole = x -> qmax - pole_tip_pos(x)\n",
    "\n",
    "    # --- Resets (bounce by flipping velocity components) ---\n",
    "    function bounce_cart_left(x)\n",
    "        x_new = copy(x)\n",
    "        x_new[1] = qmin + w/2 + 1e-9\n",
    "        x_new[3] = -e * x[3]  # flip cart velocity\n",
    "        return x_new\n",
    "    end\n",
    "\n",
    "    function bounce_cart_right(x)\n",
    "        x_new = copy(x)\n",
    "        x_new[1] = qmax - w/2 - 1e-9\n",
    "        x_new[3] = -e * x[3]\n",
    "        return x_new\n",
    "    end\n",
    "\n",
    "    function bounce_pole_left(x)\n",
    "        x_new = copy(x)\n",
    "        x_new[4] = -e * x[4]  # flip pole angular velocity\n",
    "        return x_new\n",
    "    end\n",
    "\n",
    "    function bounce_pole_right(x)\n",
    "        x_new = copy(x)\n",
    "        x_new[4] = -e * x[4]\n",
    "        return x_new\n",
    "    end\n",
    "\n",
    "    # --- Transitions ---\n",
    "    transitions = Dict(\n",
    "        :bounce_cart_left  => Transition(flow, flow, g_left_cart,  bounce_cart_left),\n",
    "        :bounce_cart_right => Transition(flow, flow, g_right_cart, bounce_cart_right),\n",
    "        #= :bounce_pole_left  => Transition(flow, flow, g_left_pole,  bounce_pole_left),\n",
    "        :bounce_pole_right => Transition(flow, flow, g_right_pole, bounce_pole_right) =#\n",
    "    )\n",
    "\n",
    "    for (_, t) in transitions\n",
    "        add_transition!(modes[:free], modes[:free], t)\n",
    "    end\n",
    "\n",
    "    return HybridSystem(nx, nu, transitions, modes)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "725c3ddf",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Solver Setup\n",
    "\"\"\"\n",
    "\n",
    "mc = 0.5\n",
    "mp = 0.5\n",
    "l = 0.5\n",
    "w = 0.5\n",
    "qmin = -0.8\n",
    "qmax = 0.8\n",
    "e = 0.5\n",
    "\n",
    "# system = get_cartpole_model(mc, mp, l)\n",
    "system = bouncing_cartpole_model(mc, mp, l, w, qmin, qmax, e)\n",
    "\n",
    "# Stage and terminal costs\n",
    "Q = 1e-5 * diagm([1e-9, 1.0, 1.0, 1.0])\n",
    "R = 1e-5 * I(system.nu)\n",
    "Qf = 4e+3 * Q\n",
    "stage(x, u) = x'*Q*x + u'*R*u\n",
    "terminal(x) = x'*Qf*x\n",
    "\n",
    "# RK4 integrator\n",
    "rk4 = ExplicitIntegrator(:rk4)\n",
    "\n",
    "# Problem parameters\n",
    "N = 50\n",
    "Δt = 0.05\n",
    "params = SiLQR.Parameters(system, stage, terminal, rk4, N, Δt)\n",
    "\n",
    "# Reference trajectory\n",
    "xref = [0.0, pi, 0.0, 0.0]\n",
    "uref = zeros(system.nu)\n",
    "us = [1e-2 * (rand(system.nu) - 0.5*ones(system.nu)) for k = 1:(N-1)]\n",
    "\n",
    "params.xrefs = [xref for k = 1:N]\n",
    "params.urefs = [uref for k = 1:(N-1)]\n",
    "\n",
    "params.x0 = 1e-1 * (rand(system.nx) - 0.5*ones(system.nx))\n",
    "params.mI = :free"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "da768601",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Solve using single shooting\n",
    "\"\"\"\n",
    "\n",
    "sol = SiLQR.Solution(params)\n",
    "sol.us .= us\n",
    "\n",
    "cache = SiLQR.Cache(params)\n",
    "@time SiLQR.solve!(\n",
    "    sol, cache, params;\n",
    "    multishoot=false, max_iter=200, αmax=0.95, stat_tol=1e-7\n",
    ")\n",
    "\n",
    "animate_cartpole(\n",
    "    sol.xs, qmin, qmax, l, w, true;\n",
    "    filename=\"ss_bouncing_cartpole.gif\"\n",
    ")\n",
    "\n",
    "bxs = reduce(vcat, sol.xs)\n",
    "plot_2d_states(\n",
    "    N, system.nx, (1,2), bxs;\n",
    "    xlim=(qmin,qmax), ylim=(-2,4),\n",
    "    xlabel=\"x\", ylabel=\"θ\"\n",
    ")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "745ceaba",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Solve using multiple shooting\n",
    "\"\"\"\n",
    "\n",
    "sol = SiLQR.Solution(params)\n",
    "sol.us .= us\n",
    "for k = 10:20:40\n",
    "    sol.xs[k] .= [qmin; zeros(3)]\n",
    "end\n",
    "for k = 15:20:35\n",
    "    sol.xs[k] .= [qmax; zeros(3)]\n",
    "end\n",
    "\n",
    "cache = SiLQR.Cache(params)\n",
    "@time SiLQR.solve!(\n",
    "    sol, cache, params;\n",
    "    multishoot=true, max_iter=200, αmax=0.95, stat_tol=1e-7\n",
    ")\n",
    "\n",
    "animate_cartpole(\n",
    "    sol.xs, qmin, qmax, l, w, true;\n",
    "    filename=\"ms_bouncing_cartpole.gif\"\n",
    ")\n",
    "\n",
    "bxs = reduce(vcat, sol.xs)\n",
    "plot_2d_states(\n",
    "    N, system.nx, (1,2), bxs;\n",
    "    xlim=(qmin,qmax), ylim=(-2,4),\n",
    "    xlabel=\"x\", ylabel=\"θ\"\n",
    ")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c034f339",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.9",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.9"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
